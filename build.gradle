plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'org.example'
version '1.4'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.example.niiefa_energo'
    mainClass = 'org.example.niiefa_energo.HelloApplication'
}

javafx {
    version = '21.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('com.fazecast:jSerialComm:[2.0.0,3.0.0)')
    implementation('com.google.guava:guava:33.0.0-jre')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

ext.os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    imageZip = project.file("${layout.buildDirectory}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'HMI_IT_300-15'
    }

    jpackage {

        if (os.macOsX) {
            icon = "src/main/resources/org/example/niiefa_energo/appIcon.icns"
        }

        mainClass = "org.example.niiefa_energo.MainApplication"

        if (os.windows) {
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut'
            ]
            icon = "src/main/resources/org/example/niiefa_energo/appIcon.ico"
        }
    }
}

jlinkZip {
    group = 'distribution'
}